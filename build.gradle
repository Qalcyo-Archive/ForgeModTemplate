/* Define Gradle plugins used in the project. */
plugins {
    id "net.minecraftforge.gradle.forge" version "6f5327738d"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.spongepowered.mixin" version "0.6-SNAPSHOT"
    id "java"
}

version = mod_ver
group = mod_group
archivesBaseName = "${mod_name} (${mod_ver})"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

/* Define Minecraft options. Such as the Forge version, run directory and game mappings. */
minecraft {
    version = forge_ver
    runDir = "run"
    replace('@NAME@', mod_name)
    replace('@VER@': mod_ver)
    replace('@ID@', mod_id)
    mappings = mc_mappings
}

/* Add available repositories to areas where dependencies can be downloaded. */
repositories {
    mavenCentral()
    maven {
        name = "SpongePowered"
        url = 'https://repo.spongepowered.org/maven/'
    }
    maven {
        name = "JitPack"
        url = 'https://jitpack.io/'
    }
}

/* Adds a configuration for shading libraries. */
configurations {
    shade
    implementation.extendsFrom(shade)
}

/* Define dependencies, also where dependencies are automatically handled. */
dependencies {
    implementation('xyz.matthewtgm:TGMLib:2.5.2')
    shade('xyz.matthewtgm:JsonTGM:2.6.3')
    shade('xyz.matthewtgm:TGMConfig:2.3')

    implementation('org.projectlombok:lombok:1.18.16')
    annotationProcessor('org.projectlombok:lombok:1.18.16')
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'launchwrapper'
        exclude module: 'commons-io'
    }
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
}

/* Disable mixin refMap warnings. */
mixin {
    disableRefMapWarning = true
    add sourceSets.main, 'mixins.template.refmap.json'
}

/* Set the duplicatesStrategy and define configs. */
jar {
    archiveName = "${archivesBaseName}.jar"
    enabled = false
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.attributes(
            'MixinConfigs': 'mixins.template.json',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'FMLCorePlugin': 'xyz.matthewtgm.template.forge.TemplateLoadingPlugin',
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true
    )
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

/* Disabled source jars. */
sourceJar {
    enabled = false
}

/* Makes the shade configuration work and re-define the duplicatesStrategy. */
shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveBaseName.set(archivesBaseName)
    archiveVersion.set('')
    archiveClassifier.set('')
}

/* Force the Gradle wrapper version and distribution type. */
wrapper {
    gradleVersion = "6.8.1"
    distributionType = Wrapper.DistributionType.ALL
}

/* Make shadow reobfuscate using SEARGE. */
reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

/* Process the mcmod.info. */
processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    inputs.property 'name', mod_name
    inputs.property 'modid', mod_id
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand (
                'version':project.version,
                'mcversion':project.minecraft.version,
                'name':mod_name,
                'modid': mod_id
        )
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/* Move the resources to the working directory for runs. */
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)
reobfJar.dependsOn(shadowJar)