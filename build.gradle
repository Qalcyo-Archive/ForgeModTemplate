buildscript { scriptHandler ->
    apply from: 'gradle/plugins.gradle', to: scriptHandler
}
plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.jetbrains.kotlin.jvm" version "1.5.31"
    id "java"
}
apply plugin: "net.minecraftforge.gradle.forge"

// Uncomment if using Mixin.
// apply from: 'gradle/mixin.gradle'

version = mod_version
group = mod_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    replace('@NAME@', mod_name)
    replace('@VER@': mod_version)
    replace('@ID@', mod_id)

    mappings = "stable_22"
    makeObfSourceJar = false

    clientRunArgs += '--tweakClass xyz.qalcyo.requisite.installer.RequisiteInstaller'
}

configurations {
    /* Creates an extra dependency configuration that implements `implementation`, */
    /* to be used later to shade libraries. */

    shade
    implementation.extendsFrom(shade)
}

repositories {
    mavenCentral()
    mavenLocal()

    /* Currently down.

    maven {
        name = 'Qalcyo'
        url = 'https://maven.qalcyo.xyz/repository/maven-public/'
    }

    */
    maven {
        name = 'Sk1erLLC'
        url = 'https://repo.sk1er.club/repository/maven-releases/'
    }
    maven {
        name = 'Jitpack'
        url = 'https://jitpack.io/'
    }
    maven {
        name = 'Spongepowered'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    /* If you'd like access to a dependency's source (for example, another mod) use the method below: */
    /* implementation('com.example.package:example:1.0.0') */

    /* If you'd rather like your dependency to be inside your built JAR file, use: */
    /* shade('com.example.package:example:1.0.0') */

    /* Currently down.

    implementation('xyz.qalcyo.crimson:Crimson-1.8.9:1.0.0')
    shade('xyz.qalcyo.crimson:CrimsonInstaller-1.8.9:1.0')
    */
}

processResources {
    inputs.property "name", mod_name
    inputs.property "version", mod_version
    inputs.property "id", mod_id
    inputs.property "mcversion", minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand(
                "name": mod_name,
                "version": mod_version,
                "id": mod_id,
                "mcversion": minecraft.version
        )
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    enabled = false
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            'ModSide': 'CLIENT',
            'ForceLoadAsMod': true,
            // 'TweakClass': 'xyz.qalcyo.crimson.installer.CrimsonInstaller'
    )
}

task moveResources {
    doLast {
        try {
            ant.move file: "${buildDir}/resources/main", todir: "${buildDir}/classes/java"
        } catch (Exception e) {
            e.printStackTrace()
        }
    }
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.shade]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude 'LICENSE.md'
    exclude 'pack.mcmeta'
    exclude 'dummyThing'
    exclude '**/module-info.class'
    exclude '*.so'
    exclude '*.dylib'
    exclude '*.dll'
    exclude '*.jnilib'
    exclude 'ibxm/**'
    exclude 'com/jcraft/**'
    exclude 'org/lwjgl/**'
    exclude 'net/java/**'

    exclude 'META-INF/proguard/**'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/versions/**'
    exclude 'META-INF/com.android.tools/**'

    exclude 'fabric.mod.json'
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)
reobfJar.dependsOn(shadowJar)